apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-allopen'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools

    defaultConfig {
        applicationId "com.apisov.percusoid"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.compileSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }

    flavorDimensions "default"

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.apisov.percusoid.OpenClass'
}

dependencies {
    /*ANDROID*/
    implementation Deps.constraintLayout
    implementation Deps.appcompat
    implementation Deps.design
    implementation Deps.room
    implementation Deps.roomRxJava
    implementation Deps.archLifecycle

    implementation 'com.shawnlin:number-picker:2.4.6'

    kapt Deps.roomCompiler
    kapt Deps.archLifecycleCompiler

    /*KOTLIN*/
    implementation Deps.anko
    implementation Deps.kotlinStdlib
    implementation Deps.koin

    // RX
    implementation Deps.rxJava
    implementation(Deps.rxKotlin) {
        exclude module: 'rxjava'
    }
    implementation(Deps.rxAndroid) {
        exclude module: 'rxjava'
    }
    /*OTHER*/
    implementation 'com.illposed.osc:javaosc-core:0.4'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    /* TESTING */
    testImplementation Deps.junit
    testImplementation Deps.mockitoCore
    testImplementation Deps.hamcrest
    testImplementation Deps.kotlinStdlib
    testImplementation Deps.kotlinTest

    testImplementation Deps.archTesting, {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }

    androidTestImplementation Deps.junit
    androidTestImplementation Deps.mockitoCore, { exclude group: 'net.bytebuddy' }

    androidTestImplementation Deps.testRunner
    androidTestImplementation Deps.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestImplementation Deps.archTesting
}